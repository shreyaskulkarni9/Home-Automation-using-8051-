ORG 0000H
    SJMP MAIN
ORG 0003H
    LJMP ENTRY_ISR      ; INT0 -> Entry
ORG 0013H
    LJMP EXIT_ISR       ; INT1 -> Exit
servo1_flag EQU 30H
servo2_flag EQU 31H
count EQU 32H
last_entry EQU 33H
last_exit EQU 34H
MAIN:
    ACALL LCD_INI
    MOV TMOD, #11H
    MOV IE, #85H
    SETB TCON.0
    SETB TCON.2
    MOV IP, #00H
    CLR IE0
    CLR IE1
    MOV P1, #00H
    MOV servo1_flag, #00H
    MOV servo2_flag, #00H
    MOV count, #00H
    MOV last_entry, #00H
    MOV last_exit, #00H
    MOV DPTR, #MSG2
    ACALL DISP_STRING
    ACALL DELAY_20MS
    MOV A, #01H
    ACALL CON_SUB
    ACALL DELAY_20MS
    SJMP MAIN_LOOP
MAIN_LOOP:
    MOV A, #01H
    ACALL CON_SUB
    ACALL DELAY_20MS

    MOV DPTR, #MSG1
    ACALL DISP_STRING
    MOV A, #20H
    ACALL DATA_SUB

    MOV A, count
    MOV B, #10
    DIV AB
    ADD A, #30H
    ACALL DATA_SUB
    MOV A, B
    ADD A, #30H
    ACALL DATA_SUB
    MOV A, #20H
    ACALL DATA_SUB
    ACALL DELAY_2S
    MOV A, count
    CJNE A, #00, LED_ON
    CLR P1.0
    SJMP SERVOS
LED_ON:
    SETB P1.0
SERVOS:
    ; Servo1 (Entry gate)
    MOV A, servo1_flag
    JZ CHECK_SERVO2
    MOV R6, #50
S1_OPEN_LOOP:
    SETB P1.1
    ACALL DELAY_2MS
    CLR P1.1
    ACALL DELAY_18_5MS
    DJNZ R6, S1_OPEN_LOOP
    MOV servo1_flag, #00H
    MOV last_entry, #00H
    MOV R6, #40
S1_CLOSE_LOOP:
    SETB P1.1
    ACALL DELAY_500US
    CLR P1.1
    ACALL DELAY_19MS
    DJNZ R6, S1_CLOSE_LOOP
    
    SJMP CHECK_SERVO2
CHECK_SERVO2:
    ; Servo2 (Exit gate)
    MOV A, servo2_flag
    JZ SERVO2_IDLE
    MOV R6, #50
S2_OPEN_LOOP:
    SETB P1.2
    ACALL DELAY_1_5MS
    CLR P1.2
    ACALL DELAY_18_5MS
    DJNZ R6, S2_OPEN_LOOP
    MOV servo2_flag, #00H
    MOV last_exit, #00H
    MOV R6, #40
S2_CLOSE_LOOP:
    SETB P1.2
    ACALL DELAY_500US
    CLR P1.2
    ACALL DELAY_19MS
    DJNZ R6, S2_CLOSE_LOOP  
    SJMP MAIN_LOOP
SERVO2_IDLE:
    SJMP MAIN_LOOP
ENTRY_ISR:
    PUSH ACC
    PUSH PSW
    PUSH B
    MOV A, last_entry
    CJNE A, #00, END_ENTRY_ISR    
    MOV R0, #50
DEBOUNCE_ENTRY:
    ACALL DELAY_1MS
    DJNZ R0, DEBOUNCE_ENTRY
    MOV A, count
    CJNE A, #255, INC_COUNT_ENTRY
    SJMP SET_ENTRY_FLAG
INC_COUNT_ENTRY:
    INC count
    MOV servo1_flag, #01H
SET_ENTRY_FLAG:
    MOV last_entry, #01H
END_ENTRY_ISR:
    CLR IE0
    POP B
    POP PSW
    POP ACC
    RETI
EXIT_ISR:
    PUSH ACC
    PUSH PSW
    PUSH B
    MOV A, last_exit
    CJNE A, #00, END_EXIT_ISR    
    MOV R0, #50
DEBOUNCE_EXIT:
    ACALL DELAY_1MS
    DJNZ R0, DEBOUNCE_EXIT
    MOV A, count
    JZ SET_EXIT_FLAG   
    DEC count
    MOV servo2_flag, #01H
SET_EXIT_FLAG:
    MOV last_exit, #01H
END_EXIT_ISR:
    CLR IE1
    POP B
    POP PSW
    POP ACC
    RETI
DELAY_1MS:
    MOV R2, #2
D1MS_LOOP:
    MOV R1, #250
    DJNZ R1, $
    DJNZ R2, D1MS_LOOP
    RET
DELAY_1_5MS:
    MOV R2, #3
D15MS_LOOP:
    MOV R1, #250
    DJNZ R1, $
    DJNZ R2, D15MS_LOOP
    RET
DELAY_2MS:
    MOV R2, #4
D2MS_LOOP:
    MOV R1, #250
    DJNZ R1, $
    DJNZ R2, D2MS_LOOP
    RET
DELAY_18_5MS:
    MOV R3, #37
D185MS_LOOP:
    ACALL DELAY_500US
    DJNZ R3, D185MS_LOOP
    RET
DELAY_500US:
    MOV R4, #250
D500US_LOOP:
    NOP
    DJNZ R4, D500US_LOOP
    RET
DELAY_20MS:
    MOV R3, #40
D20MS_LOOP:
    ACALL DELAY_500US
    DJNZ R3, D20MS_LOOP
    RET
DELAY_200MS:
    MOV R7, #100
D200MS_LOOP:
    ACALL DELAY_1MS
    DJNZ R7, D200MS_LOOP
    RET
DELAY_300MS:
    MOV R7, #150
D300MS_LOOP:
    ACALL DELAY_1MS
    DJNZ R7, D300MS_LOOP
    RET
DELAY_500MS:
    MOV R7, #250
D500MS_LOOP:
    ACALL DELAY_1MS
    DJNZ R7, D500MS_LOOP
    RET
DELAY_1S:
    MOV R7, #250
D1S_LOOP:
    ACALL DELAY_1MS
    DJNZ R7, D1S_LOOP
    ACALL DELAY_500MS
    RET
DELAY_2S:
    ACALL DELAY_1S
    ACALL DELAY_1S
    RET

DELAY_19MS:
    MOV R3, #38
D19MS_LOOP:
    ACALL DELAY_500US
    DJNZ R3, D19MS_LOOP
    RET
LCD_INI:
    MOV A, #38H
    ACALL CON_SUB
    ACALL DELAY_20MS
    MOV A, #01H
    ACALL CON_SUB
    ACALL DELAY_20MS
    MOV A, #0EH
    ACALL CON_SUB
    ACALL DELAY_20MS
    MOV A, #06H
    ACALL CON_SUB
    ACALL DELAY_20MS
    MOV A, #80H
    ACALL CON_SUB
    ACALL DELAY_20MS
    RET
CON_SUB:
    MOV P2, A
    CLR P3.7
    SETB P3.5
    ACALL DELAY_1MS
    CLR P3.5
    RET
DATA_SUB:
    MOV P2, A
    SETB P3.7
    SETB P3.5
    ACALL DELAY_1MS
    CLR P3.5
    RET
DISP_STRING:
    CLR A
    MOVC A, @A+DPTR
    JZ DISP_END
    ACALL DATA_SUB
    ACALL DELAY_20MS
    INC DPTR
    SJMP DISP_STRING
DISP_END:
    RET
ORG 0300H
MSG1: DB "Count:",0
MSG2: DB "System Ready",0

END